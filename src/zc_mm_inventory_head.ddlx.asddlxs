@Metadata.layer: #CORE
@UI.headerInfo.typeName: 'Cockpit de Inventário'
@UI.headerInfo.typeNamePlural: 'Cockpit de Inventário'
@UI.headerInfo.title.type: #STANDARD
@UI.headerInfo.title.label: 'Inventário'
@UI.headerInfo.title.value: 'DocumentNo'
@UI.headerInfo.typeImageUrl: 'sap-icon://measurement-document'

@UI.lineItem: [{criticality: 'StatusCrit'}]
@UI.updateHidden: #(UpdateHidden)
@UI.presentationVariant: [{sortOrder: [{ by: 'DocumentNo', direction: #DESC }],
                           visualizations: [{type: #AS_LINEITEM}]}]

annotate view ZC_MM_INVENTORY_HEAD with 
{
  // ------------------------------------------------------
  // Header information
  // ------------------------------------------------------

  @UI.facet                 : [ { id            : 'DataPointCountId',
                                 purpose        : #HEADER,
                                 type           : #DATAPOINT_REFERENCE,
                                 targetQualifier: 'DataPointCountId',
                                 position       : 01 },

                               { id             : 'DataPointStatusId',
                                 purpose        : #HEADER,
                                 type           : #DATAPOINT_REFERENCE,
                                 targetQualifier: 'DataPointStatusId',
                                 position       : 02 },

                               { id             : 'Dados',
                                 purpose        : #STANDARD,
                                 type           : #COLLECTION,
                                 label          : 'Dados Gerais',
                                 position       : 10 },

                               { id             : 'DadosGeral',
                                 purpose        : #STANDARD,
                                 label          : 'Dados Gerais',
                                 parentId       : 'Dados',
                                 type           : #FIELDGROUP_REFERENCE,
                                 targetQualifier: 'DadosGeral',
                                 position       : 11 },

                               { id             : 'DadosRegistro',
                                 purpose        : #STANDARD,
                                 label          : 'Dados de Modificação',
                                 parentId       : 'Dados',
                                 type           : #FIELDGROUP_REFERENCE,
                                 targetQualifier: 'DadosRegistro',
                                 position       : 12 },

                               { id             : 'Itens',
                                 purpose        : #STANDARD,
                                 type           : #LINEITEM_REFERENCE,
                                 label          : 'Itens',
                                 position       : 20,
                                 targetElement  : '_Item'},

                               { id             : 'Log',
                                 purpose        : #STANDARD,
                                 type           : #LINEITEM_REFERENCE,
                                 label          : 'Logs',
                                 position       : 30,
                                 targetElement  : '_Log'}
              ]

  // ------------------------------------------------------
  //Button information
  // ------------------------------------------------------

  @UI.lineItem              : [{ position: 10, type: #FOR_ACTION, dataAction: 'liberar', label: 'Liberar' },
                               { position: 20, type: #FOR_ACTION, dataAction: 'cancelar', label: 'Encerrar' }]

  @UI.identification        : [{ position: 10, type: #FOR_ACTION, dataAction: 'liberar', label: 'Liberar' },
                               { position: 20, type: #FOR_ACTION, dataAction: 'cancelar', label: 'Encerrar' }]

  // ------------------------------------------------------
  // Field information
  // ------------------------------------------------------

  @UI.hidden                : true

  @EndUserText.label        : 'ID do documento'
  DocumentId;

  @UI.selectionField        : [{ position: 10 }]
  @UI.lineItem              : [{ position: 10 }]
  @UI.fieldGroup            : [{ position: 10, qualifier: 'DadosGeral' }]

  @EndUserText.label        : 'Doc. Pré Inventário'
  DocumentNo;

  @UI.selectionField        : [{ position: 20 }]
  @UI.lineItem              : [{ position: 20 }]
  @UI.fieldGroup            : [{ position: 20, qualifier: 'DadosGeral' }]
  @UI.dataPoint             : { qualifier: 'DataPointCountId', title: 'Id contagem' }

  @EndUserText.label        : 'ID da contagem'
  CountId;

  @UI.selectionField        : [{ position: 30 }]
  @UI.lineItem              : [{ position: 30 }]
  @UI.fieldGroup            : [{ position: 30, qualifier: 'DadosGeral' }]

  @EndUserText.label        : 'Data da contagem'
  CountDate;

  @Consumption.valueHelpDefinition: [{ entity: {name: 'ZI_MM_VH_INVENTORY_STATUS', element: 'Status' },
                                       qualifier: 'ZI_MM_VH_INVENTORY_STATUS', useForValidation: true  }]

  @UI.selectionField        : [{ position: 40 } ]
  @UI.lineItem              : [{ position: 40, criticality: 'StatusCrit' } ]
  @UI.fieldGroup            : [{ position: 40, criticality: 'StatusCrit' , qualifier: 'DadosGeral' }]
  @UI.dataPoint             : { qualifier: 'DataPointStatusId', title: 'Status', criticality: 'StatusCrit' }

  @EndUserText.label        : 'Status'
  @UI.textArrangement: #TEXT_ONLY
  StatusId;

  @UI.hidden                : true

  @EndUserText.label        : 'Descrição do Status'
  StatusText;

  @UI.hidden                : true

  @EndUserText.label        : 'Criticalidade do Status'
  StatusCrit;

  @UI.selectionField        : [{ position: 45 }]
  @UI.lineItem              : [{ position: 45 }]
  @UI.fieldGroup            : [{ position: 30, qualifier: 'DadosGeral' }]

  @Consumption.valueHelpDefinition: [{ entity: {name: 'ZI_MM_CE_VH_PLANT', element: 'Plant' },
                                       additionalBinding: [{ element: 'PlantName', localElement: 'PlantName' }],
                                       qualifier: 'ZI_MM_CE_VH_PLANT', useForValidation: true  }]

  @EndUserText.label        : 'Centro'
  Plant; 

  @Consumption.valueHelpDefinition: [{ entity: {name: 'ZI_MM_CE_VH_PLANT', element: 'PlantName' },
                                       additionalBinding: [{ element: 'Plant', localElement: 'Plant' }],
                                       qualifier: 'ZI_MM_CE_VH_PLANT', useForValidation: true  }]

  @UI.hidden                : true
  @EndUserText.label        : 'Nome do centro'
  PlantName;

  @UI.multiLineText         : true
  @UI.lineItem              : [{ position: 70 } ]
  @UI.fieldGroup            : [{ position: 70, qualifier: 'DadosGeral' }]

  @EndUserText.label        : 'Descrição'
  Description;

  @UI.fieldGroup            : [{ position: 10, qualifier: 'DadosRegistro' }]

  @EndUserText.label        : 'Criado por'
  CreatedBy;

  @UI.hidden: true

  CreatedByName;

  @UI.fieldGroup            : [{ position: 20, qualifier: 'DadosRegistro' }]

  @EndUserText.label        : 'Criado em'
  CreatedAt;

  @UI.fieldGroup            : [{ position: 30, qualifier: 'DadosRegistro' }]

  @EndUserText.label        : 'Modificado por'
  LastChangedBy;

  @UI.hidden: true

  LastChangedByName;

  @UI.fieldGroup            : [{ position: 40, qualifier: 'DadosRegistro' }]

  @EndUserText.label        : 'Modificado em'
  LastChangedAt;

  @UI.fieldGroup            : [{ position: 50, qualifier: 'DadosRegistro' }]

  @EndUserText.label        : 'Registro'
  LocalLastChangedAt;

}